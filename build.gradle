buildscript {
	ext {
		springBootVersion = '2.0.4.RELEASE'
	}
	repositories {
		mavenCentral()
        jcenter()
	}
	dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:1.0.6.RELEASE"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
        classpath "com.github.ksoichiro:gradle-build-info-plugin:0.2.0"
    }
}

apply plugin: 'java-library'
apply plugin: 'idea'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.github.ksoichiro.build.info"

group = 'pl.sagiton'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
    }
    dependencies {
        dependency group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.18.0'
    }
}

dependencies {
	compileOnly group: 'org.projectlombok', name: 'lombok'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    runtimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    api group: 'org.springframework', name: 'spring-web'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter'
    testImplementation group: 'org.mockito', name: 'mockito-core'
}

generateBuildInfo {
    doLast {
        (project.tasks.jar as Jar).manifest.attributes(
            BUILD_HOST:     InetAddress.localHost.canonicalHostName,
            BUILD_USERNAME: System.env.USER
        )
        if (System.env.BUILD_NUMBER) { //it should be enough to check if we're running on Jenkins
            (project.tasks.jar as Jar).manifest.attributes(
                BUILD_NUMBER: System.env.BUILD_NUMBER,
                BUILD_ID:     System.env.BUILD_ID,
                BUILD_URL:    System.env.BUILD_URL,
                NODE_NAME:    System.env.NODE_NAME,
                JOB_NAME:     System.env.JOB_NAME,
                JENKINS_URL:  System.env.JENKINS_URL
            )
        }
    }
}

/*
* Remember to provide credentials for Nexus!
* These are configured by setting project properties: nexusUsername and nexusPassword.
*
* You can do that with properties file (in repo or in your home directory) but remember not to commit
* that file!
*
* In CI environment you can use environment variables:
*
*     export ORG_GRADLE_PROJECT_nexusUsername=...
*     export ORG_GRADLE_PROJECT_nexusPassword=...
*
* See:
* https://docs.gradle.org/current/userguide/userguide_single.html?_ga=2.157422650.1235070294.1533801411-1191488433.1516802291#sec:project_properties
*/
nexus {
    repositoryUrl = 'https://nexus.sagiton.pl/repository/maven-releases/'
    snapshotRepositoryUrl = 'https://nexus.sagiton.pl/repository/maven-snapshots/'
}